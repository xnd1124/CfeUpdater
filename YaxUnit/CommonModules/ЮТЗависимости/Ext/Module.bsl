//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2024 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ПрограммныйИнтерфейс

// Указывает зависимость теста (набора или модуля) от файлов проекта.
// Гарантирует доступность файлов как на клиенте, так и на сервере.
// 
// Параметры:
//  ПутьКФайлам - Строка - Относительный путь к файлу проекта (относительно настройки projectPath)
// 
// Возвращаемое значение:
//  CommonModule.ЮТТесты - Файлы проекта
Функция ФайлыПроекта(ПутьКФайлам) Экспорт
	
	ОписаниеЗависимости = ЮТФабрика.НовоеОписаниеЗависимости();
	ОписаниеЗависимости.Идентификатор = "ФайлыПроекта";
	ОписаниеЗависимости.МетодРеализации = "ЮТРазрешениеЗависимостейСлужебныйКлиент.ФайлыПроекта";
	ОписаниеЗависимости.Параметры.Добавить(ПутьКФайлам);
	ОписаниеЗависимости.Асинхронный = Истина;
	
	Возврат ЮТЗависимостиСлужебный.ДобавитьЗависимостьПриРегистрации(ОписаниеЗависимости);
	
КонецФункции

// Указывает зависимость теста (набора или модуля) от фабрики XDTO, создаваемой на основании файлов.
// Создает и кэширует фабрику, предоставляя к доступ из любого серверного теста
// 
// Параметры:
//  ПутьКФайлам - Строка - Относительный путь к файлу проекта (относительно настройки projectPath)
// 
// Возвращаемое значение:
//  CommonModule.ЮТТесты - Файлы проекта
Функция ФабрикаXDTO(ПутьКФайлам) Экспорт
	
	ФайлыПроекта(ПутьКФайлам);
	
	ОписаниеЗависимости = ЮТФабрика.НовоеОписаниеЗависимости();
	ОписаниеЗависимости.Идентификатор = "ФабрикаXDTO";
	ОписаниеЗависимости.МетодРеализации = "ЮТРазрешениеЗависимостейСлужебныйВызовСервера.ФабрикаXDTO";
	ОписаниеЗависимости.Параметры.Добавить(ПутьКФайлам);
	ОписаниеЗависимости.ОбработкаНаСервере = Истина;
	ОписаниеЗависимости.ДоступностьНаКлиенте = Ложь;
	
	Возврат ЮТЗависимостиСлужебный.ДобавитьЗависимостьПриРегистрации(ОписаниеЗависимости);
	
КонецФункции

#КонецОбласти
